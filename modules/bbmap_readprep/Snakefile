#!/usr/bin/env python3

from snakemake.logging import logger
from pathlib import Path

###########
# GLOBALS #
###########

# containers
bbmap = "docker://quay.io/biocontainers/bbmap:39.01--h92535d8_1"

# set up directories
outdir = Path(config["outdir"] if "outdir" in config else ".")
logger.debug(f"outdir: {outdir}")
logdir = Path(outdir, "logs")

# catch input
adaptors = config["adaptors"]
logger.info(f"bbmap_readprep: trimming adaptors from {adaptors}")
reads_directory = config["reads_directory"]
logger.info(f"bbmap_readprep: using input fastq files from {reads_directory}")

#########
# RULES #
#########


rule target:
    input:
        expand(
            Path(outdir, "{{sample}}.{readset}.fastq.gz"),
            readset=["r1", "r2", "unpaired"],
        ),


rule trim:
    input:
        pipe=Path(outdir, "{sample}.repair.fastq"),
        adaptors=adaptors,
    output:
        r1=Path(outdir, "{sample}.r1.fastq.gz"),
        r2=Path(outdir, "{sample}.r2.fastq.gz"),
        unpaired=Path(outdir, "{sample}.unpaired.fastq.gz"),
    log:
        log=Path(logdir, "trim", "{sample}.log"),
        stats=Path(logdir, "{sample}.stats.txt"),
    threads: 2
    resources:
        time=lambda wildcards, attempt: 10 * attempt,
        mem_mb=lambda wildcards, attempt: 2e3 * attempt,
    container:
        bbmap
    shell:
        "bbduk.sh "
        "-Xmx{resources.mem_mb}m "
        "forcetrimmod=5 "
        "in={input.pipe} "
        "int=t "
        "ktrim=r k=23 mink=11 hdist=1 tpe tbo "
        "out={output.r1} "
        "out2={output.r2} "
        "outs={output.unpaired} "
        "ref={input.adaptors} "
        "stats={log.stats} "
        "threads={threads} "
        "zl=9 "
        "2> {log.log} "


# double check pairing
rule repair:
    input:
        r1=Path(reads_directory, "{sample}.r1.fastq.gz"),
        r2=Path(reads_directory, "{sample}.r2.fastq.gz"),
    output:
        pipe(Path(outdir, "{sample}.repair.fastq")),
    log:
        Path(logdir, "repair", "{sample}.log"),
    threads: 1
    resources:
        time=lambda wildcards, attempt: 10 * attempt,
        mem_mb=lambda wildcards, attempt: 4e3 * attempt,
    shell:
        "repair.sh "
        "-Xmx{resources.mem_mb}m "
        "-Xms100m "
        "in={input.r1} "
        "in2={input.r2} "
        "out=stdout.fastq "
        "outs=/dev/null "
        "repair=t "
        "tossbrokenreads=t "
        "tossjunk=t "
        ">> {output} "
        "2> {log}"


rule collect_input:
    input:
        Path(reads_directory, "{sample}.r{r}.fq.gz")
    output:
        Path(reads_directory, "{sample}.r{r}.fastq.gz")
    handover:
        True
    shell:
        "ln -s $(readlink -f {input}) $(readlink -f {output})"